plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "java"
    id "eclipse"
}

group = 'net.sargue'
archivesBaseName = 'java-time-jsptags'
version = '1.1.4'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    testCompile.extendsFrom compileOnly
}

dependencies {
    compileOnly 'javax.servlet:javax.servlet-api:3.0.1'
    compileOnly 'javax.servlet.jsp:javax.servlet.jsp-api:2.2.1'
    compileOnly 'javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api:1.2.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.springframework:spring-test:4.1.7.RELEASE'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Java 8 java.time JSP tags',
                   'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// install {
//     repositories.mavenInstaller {
//         pom.project {
//             name 'Java 8 java.time JSP tags'
//             description 'JSP tag support for Java 8 java.time (JSR-310)'
//             url 'https://github.com/sargue/java-time-jsptags'
// 
//             scm {
//                 connection 'scm:git:git@github.com:sargue/java-time-jsptags.git'
//                 developerConnection 'scm:git:git@github.com:sargue/java-time-jsptags.git'
//                 url 'git@github.com:sargue/java-time-jsptags.git'
//             }
// 
//             licenses {
//                 license {
//                     name 'The Apache License, Version 2.0'
//                     url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
//                 }
//             }
// 
//             developers {
//                 developer {
//                     id 'sargue'
//                     name 'Sergi Baila'
//                     email 'sargue@gmail.com'
//                 }
//             }
//         }
//     }
// }

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'net.sargue:java-time-jsptags'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sargue/java-time-jsptags'
        version {
            name = project.version
            desc = 'JSP tag support for Java 8 java.time (JSR-310)'

            gpg {
                sign = true
                passphrase = project.hasProperty('BINTRAY_GPG') ? BINTRAY_GPG : ''
            }
        }
    }
}

eclipse {
    classpath {
        file {
            // remove entries added due to issue with testsets                                                                      
            // https://github.com/unbroken-dome/gradle-testsets-plugin/issues/77                                                    
            whenMerged {                                                                                                            
                entries.removeAll{it.kind == "lib" && (it.path.endsWith("build/classes/java/test") || it.path.endsWith("build/resources/test"))}
            }                                                                                                                       
        }
    }
}