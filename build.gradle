plugins {
    id "java-library"
    id "maven-publish"
    id 'signing'
}

group = 'net.sargue'
version = '2.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

allprojects {
    tasks.withType(Javadoc) {
        options.addBooleanOption('Xdoclint:none', true)
    }
}

repositories {
    mavenCentral()
    // for snapshot release of spring that includes jakarta classes
    maven {
      url "https://repo.spring.io/milestone"
    }    
}

dependencies {
    api(group: "jakarta.servlet.jsp", name: "jakarta.servlet.jsp-api", version: "3.0.0")
    api(group: "jakarta.servlet.jsp.jstl", name: "jakarta.servlet.jsp.jstl-api", version: "2.0.0")
    

    testImplementation(group: "junit", name: "junit", version: "4.13.2")
    testImplementation(group: "org.springframework", name: "spring-test", version: "6.0.0-M2")
}

jar {
    manifest {
        attributes('Implementation-Title': 'Java 8 java.time JSP tags',
                   'Implementation-Version': archiveVersion)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Java 8 java.time JSP tags'
                description = 'JSP tag support for Java 8 java.time (JSR-310)'
                url = 'https://github.com/sargue/java-time-jsptags'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'sargue'
                        name = 'Sergi Baila'
                        email = 'sergibaila@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:sargue/java-time-jsptags.git'
                    developerConnection = 'scm:git:git@github.com:sargue/java-time-jsptags.git'
                    url = 'git@github.com:sargue/java-time-jsptags.git'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
